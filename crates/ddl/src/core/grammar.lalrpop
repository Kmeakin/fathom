use codespan::{ByteIndex, FileId, Span};
use codespan_reporting::diagnostic::Diagnostic;
use std::sync::Arc;

use crate::core::{Alias, Item, Label, Module, StructType, Term, TypeField};
use crate::diagnostics;
use crate::lexer::Token;

grammar(file_id: FileId, report: &mut dyn FnMut(Diagnostic));

extern {
    type Location = ByteIndex;
    type Error = Diagnostic;

    enum Token {
        "identifier" => Token::Identifier(<String>),
        "doc comment" => Token::DocComment(<String>),

        "item" => Token::Item,
        "struct" => Token::Struct,

        "{" => Token::OpenBrace,
        "}" => Token::CloseBrace,
        "(" => Token::OpenParen,
        ")" => Token::CloseParen,

        "!" => Token::Bang,
        ":" => Token::Colon,
        "," => Token::Comma,
        "=" => Token::Equals,
        ";" => Token::Semi,
    }
}

pub Module: Module = {
    <items: Item*> => Module { file_id, items },
};

Item: Item = {
    <doc: "doc comment"*>
    <start: @L> <name: "identifier"> "=" <term: Term> ";" <end: @R> => {
        let span = Span::new(start, end);
        let doc = Arc::from(doc);
        let name = Label(name);

        Item::Alias(Alias { span, doc, name, term })
    },
    <docs: "doc comment"*>
    <start: @L> "struct" <name: "identifier">  "{"
        <fields: (<Field> ",")*>
        <last: Field?>
    "}" <end: @R> => {
        let span = Span::new(start, end);
        let doc = Arc::from(docs);
        let mut fields = fields;
        fields.extend(last);
        let name = Label(name);

        Item::Struct(StructType { span, doc, name, fields })
    },
};

Field: TypeField = {
    <doc: "doc comment"*>
    <start: @L> <name: "identifier"> ":" <term: Term> => {
        let name = Label(name);
        let doc = Arc::from(doc);

        TypeField { doc, start, name, term }
    },
};

Term: Term = {
    TermAtomic,
    <term: TermAtomic> ":" <ty: Term> => Term::Ann(Arc::new(term), Arc::new(ty)),
};

TermAtomic: Term = {
    "(" <term: Term> ")" => term,
    <start: @L> "!" <end: @R> => Term::Error(Span::new(start, end)),
    <start: @L> "item" <name: "identifier"> <end: @R> => Term::Item(Span::new(start, end), Label(name)),
    <start: @L> <name: "identifier"> <end: @R> => {
        let span = Span::new(start, end);
        match name.as_ref() {
            "Type" => Term::Type(span),
            "U8" => Term::U8(span),
            "U16Le" => Term::U16Le(span),
            "U16Be" => Term::U16Be(span),
            "U32Le" => Term::U32Le(span),
            "U32Be" => Term::U32Be(span),
            "U64Le" => Term::U64Le(span),
            "U64Be" => Term::U64Be(span),
            "S8" => Term::S8(span),
            "S16Le" => Term::S16Le(span),
            "S16Be" => Term::S16Be(span),
            "S32Le" => Term::S32Le(span),
            "S32Be" => Term::S32Be(span),
            "S64Le" => Term::S64Le(span),
            "S64Be" => Term::S64Be(span),
            "F32Le" => Term::F32Le(span),
            "F32Be" => Term::F32Be(span),
            "F64Le" => Term::F64Le(span),
            "F64Be" => Term::F64Be(span),
            _ => {
                report(diagnostics::bug::unknown_global(file_id, &name, span));
                Term::Error(span)
            },
        }
    },
};
