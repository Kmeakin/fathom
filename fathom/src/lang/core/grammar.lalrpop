use std::sync::Arc;

use crate::lang::Ranged;
use crate::lang::core::{Alias, Constant, ItemData, Module, StructType, Term, TermData, TypeField};
use crate::lexer::Token;
use crate::literal;
use crate::reporting::{LexerMessage, Message};

grammar(file_id: usize, messages: &mut Vec<Message>);

extern {
    type Location = usize;
    type Error = LexerMessage;

    enum Token {
        "doc comment" => Token::DocComment(<String>),
        "inner doc comment" => Token::InnerDocComment(<String>),
        "identifier" => Token::Identifier(<String>),
        "numeric literal" => Token::NumberLiteral(<String>),
        "string literal" => Token::StringLiteral(<String>),
        "character literal" => Token::CharLiteral(<String>),

        "bool_elim" => Token::BoolElim,
        "else" => Token::Else,
        "f32" => Token::F32,
        "f64" => Token::F64,
        "Format" => Token::Format,
        "global" => Token::Global,
        "if" => Token::If,
        "int" => Token::Int,
        "int_elim" => Token::IntElim,
        "item" => Token::Item,
        "match" => Token::Match,
        "struct" => Token::Struct,
        "Type" => Token::Type,

        "{" => Token::OpenBrace,
        "}" => Token::CloseBrace,
        "(" => Token::OpenParen,
        ")" => Token::CloseParen,

        "!" => Token::Bang,
        ":" => Token::Colon,
        "," => Token::Comma,
        "=" => Token::Equals,
        "=>" => Token::EqualsGreater,
        "->" => Token::HyphenGreater,
        ";" => Token::Semi,
    }
}

pub Module: Module = {
    <doc: "inner doc comment"*> <items: Ranged<ItemData>*> => {
        let doc = Arc::from(doc);

        Module { file_id, doc, items }
    },
};

ItemData: ItemData = {
    <doc: "doc comment"*> <name: "identifier"> "=" <term: Term> ";" => {
        let doc = Arc::from(doc);
        let term = Arc::new(term);

        ItemData::Alias(Alias { doc, name, term })
    },
    <docs: "doc comment"*>
    "struct" <name: "identifier">  "{"
        <mut fields: (<Field> ",")*>
        <last: Field?>
    "}" => {
        let doc = Arc::from(docs);
        fields.extend(last);

        ItemData::Struct(StructType { doc, name, fields })
    },
};

Field: TypeField = {
    <doc: "doc comment"*> <name: "identifier"> ":" <term: Term> => {
        let doc = Arc::from(doc);
        let term = Arc::new(term);

        TypeField { doc, name, term }
    },
};

#[inline] Term: Term = Ranged<TermData>;
#[inline] ArrowTerm: Term = Ranged<ArrowTermData>;
#[inline] AppTerm: Term = Ranged<AppTermData>;
#[inline] AtomicTerm: Term = Ranged<AtomicTermData>;

TermData: TermData = {
    ArrowTermData,
    <term: ArrowTerm> ":" <ty: Term> => TermData::Ann(Arc::new(term), Arc::new(ty)),
};

ArrowTermData: TermData = {
    AppTermData,
    <param_type: AppTerm> "->" <body_type: ArrowTerm> => {
        TermData::FunctionType(Arc::new(param_type), Arc::new(body_type))
    },
};

AppTermData: TermData = {
    AtomicTermData,
    <head: AtomicTerm> <argument: AtomicTerm> => {
        TermData::FunctionElim(Arc::new(head), Arc::new(argument))
    },
};

AtomicTermData: TermData = {
    "(" <term: TermData> ")" => term,
    "!" => TermData::Error,
    "global" <name: "identifier"> => TermData::Global(name),
    "item" <name: "identifier"> => TermData::Item(name),
    "Type" => TermData::TypeType,
    "bool_elim" <head: Term> "{" <if_true: Term> "," <if_false: Term> "}" => {
        TermData::BoolElim(Arc::new(head), Arc::new(if_true), Arc::new(if_false))
    },
    "int_elim" <head: Term> "{"
        <branches: (<@L> <"numeric literal"> <@R> "=>" <Term> ",")*> <default: Term>
    "}" => {
        let branches = branches
            .into_iter()
            .filter_map(|(start, literal, end, term)| Some((
                literal::State::new(file_id, start..end, literal.as_str(), messages).number_to_big_int()?,
                Arc::new(term),
            ))).collect();

        TermData::IntElim(Arc::new(head), branches, Arc::new(default))
    },
    "int" <start: @L> <literal: "numeric literal"> <end: @R> => {
        match literal::State::new(file_id, start..end, literal.as_str(), messages).number_to_big_int() {
            Some(value) => TermData::Constant(Constant::Int(value)),
            None => TermData::Error,
        }
    },
    "f32" <start: @L> <literal: "numeric literal"> <end: @R> => {
        match literal::State::new(file_id, start..end, literal.as_str(), messages).number_to_float() {
            Some(value) => TermData::Constant(Constant::F32(value)),
            None => TermData::Error,
        }
    },
    "f64" <start: @L> <literal: "numeric literal"> <end: @R> => {
        match literal::State::new(file_id, start..end, literal.as_str(), messages).number_to_float() {
            Some(value) => TermData::Constant(Constant::F64(value)),
            None => TermData::Error,
        }
    },
    "Format" => TermData::FormatType,
};

#[inline] Ranged<T>: Ranged<T> = {
    <start: @L> <data: T> <end: @R> => Ranged::new(start..end, data),
};
