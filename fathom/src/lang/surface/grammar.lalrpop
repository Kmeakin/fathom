use std::sync::Arc;

use crate::lang::Ranged;
use crate::lang::surface::{Alias, ItemData, Module, Pattern, PatternData, StructType, Term, TermData, TypeField};
use crate::lexer::Token;
use crate::reporting::LexerMessage;

grammar(file_id: usize);

extern {
    type Location = usize;
    type Error = LexerMessage;

    enum Token {
        "doc comment" => Token::DocComment(<String>),
        "inner doc comment" => Token::InnerDocComment(<String>),
        "identifier" => Token::Identifier(<String>),
        "numeric literal" => Token::NumberLiteral(<String>),
        "string literal" => Token::StringLiteral(<String>),
        "character literal" => Token::CharLiteral(<String>),

        "bool_elim" => Token::BoolElim,
        "else" => Token::Else,
        "f32" => Token::F32,
        "f64" => Token::F64,
        "Format" => Token::Format,
        "global" => Token::Global,
        "if" => Token::If,
        "int" => Token::Int,
        "int_elim" => Token::IntElim,
        "item" => Token::Item,
        "match" => Token::Match,
        "struct" => Token::Struct,
        "Type" => Token::Type,

        "{" => Token::OpenBrace,
        "}" => Token::CloseBrace,
        "(" => Token::OpenParen,
        ")" => Token::CloseParen,

        "!" => Token::Bang,
        ":" => Token::Colon,
        "," => Token::Comma,
        "=" => Token::Equals,
        "=>" => Token::EqualsGreater,
        "->" => Token::HyphenGreater,
        ";" => Token::Semi,
    }
}

pub Module: Module = {
    <doc: "inner doc comment"*> <items: Ranged<ItemData>*> => {
        let doc = Arc::from(doc);

        Module { file_id, doc, items }
    },
};

ItemData: ItemData = {
    <doc: "doc comment"*>
    <name: Ranged<"identifier">> <type_: (":" <Term>)?> "=" <term: Term> ";" => {
        let doc = Arc::from(doc);

        ItemData::Alias(Alias { doc, name, type_, term })
    },
    <doc: "doc comment"*>
    "struct" <name: Ranged<"identifier">> "{"
        <mut fields: (<Field> ",")*>
        <last: Field?>
    "}" => {
        let doc = Arc::from(doc);
        fields.extend(last);

        ItemData::Struct(StructType { doc, name, fields })
    },
};

Field: TypeField = {
    <docs: "doc comment"*>
    <name: Ranged<"identifier">> ":" <term: Term> => {
        TypeField { doc: Arc::from(docs), name, term }
    },
};

#[inline] Pattern: Pattern = Ranged<PatternData>;

PatternData: PatternData = {
    <name: "identifier"> => PatternData::Name(name),
    <literal: "numeric literal"> => PatternData::NumberLiteral(literal),
};

#[inline] Term: Term = Ranged<TermData>;
#[inline] ArrowTerm: Term = Ranged<ArrowTermData>;
#[inline] AppTerm: Term = Ranged<AppTermData>;
#[inline] AtomicTerm: Term = Ranged<AtomicTermData>;

TermData: TermData = {
    ArrowTermData,
    <term: ArrowTerm> ":" <ty: Term> => TermData::Ann(Box::new(term), Box::new(ty)),
};

ArrowTermData: TermData = {
    AppTermData,
    <param_type: AppTerm> "->" <body_type: ArrowTerm> => {
        TermData::FunctionType(Box::new(param_type), Box::new(body_type))
    },
};

AppTermData: TermData = {
    AtomicTermData,
    <head: AtomicTerm> <arguments: AtomicTerm+> => {
        TermData::FunctionElim(Box::new(head), arguments)
    },
};

AtomicTermData: TermData = {
    "(" <term: TermData> ")" => term,
    <name: "identifier"> => TermData::Name(name),
    "Type" => TermData::TypeType,
    <literal: "numeric literal"> => TermData::NumberLiteral(literal),
    "if" <head: Term> "{" <if_true: Term> "}" "else" "{" <if_false: Term> "}" => {
        TermData::If(Box::new(head), Box::new(if_true), Box::new(if_false))
    },
    "match" <head: Term> "{" <mut branches: (<Pattern> "=>" <Term> ",")*> <last: (<Pattern> "=>" <Term>)?>"}" => {
        branches.extend(last);
        TermData::Match(Box::new(head), branches)
    },
    "Format" => TermData::FormatType,
};

#[inline] Ranged<T>: Ranged<T> = {
    <start: @L> <data: T> <end: @R> => Ranged::new(start..end, data),
};
