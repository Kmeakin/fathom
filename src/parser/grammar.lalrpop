use std::str::FromStr;

use ast::{Definition, Field, Expr, Type};
use env::Env;
use source::BytePos as B;

grammar(env: &Env);

pub Definitions: Vec<Definition> = {
    Definition*,
};

Definition: Definition = {
    <lo: @L> <name: Ident> "=" <ty: Type> ";" <hi: @R> => {
        Definition::new((B(lo), B(hi)), name, ty)
    },
};

Ident: &'input str = {
    r"[_a-zA-Z][_a-zA-Z0-9]*",
};

AtomicType: Type = {
    <lo: @L> <name: Ident> <hi: @R> => {
        match env.lookup_ty(&name) {
            Some(ty) => ty.clone(),
            None => Type::ident((B(lo), B(hi)), name),
        }
    },
    "(" <Type> ")",
    <lo: @L> "{" <fields: (<Field> ",")*> <last: Field?> "}" <hi: @R> => {
        let mut fields = fields;
        fields.extend(last);
        Type::struct_((B(lo), B(hi)), fields)
    },
    <lo: @L> "[" <elem: Type> ";" <size: Expr> "]" <hi: @R> => {
        Type::array((B(lo), B(hi)), Box::new(elem), size)
    },
};

pub Type: Type = {
    AtomicType,
    <lo: @L> "|"? <tys: (<AtomicType> "|")+> <last: AtomicType> <hi: @R> => {
        let mut tys = tys;
        tys.push(last);
        Type::union((B(lo), B(hi)), tys)
    },
};

pub Expr = AddExpr;

AddExpr : Expr = {
    MulExpr,
    <lo: @L> <lhs: MulExpr> "+" <rhs: MulExpr> <hi: @R> => {
        Expr::add((B(lo), B(hi)), lhs, rhs)
    },
    <lo: @L> <lhs: MulExpr> "-" <rhs: MulExpr> <hi: @R> => {
        Expr::sub((B(lo), B(hi)), lhs, rhs)
    },
};

MulExpr : Expr = {
    AtomicExpr,
    <lo: @L> <lhs: AtomicExpr> "*" <rhs: AtomicExpr> <hi: @R> => {
        Expr::mul((B(lo), B(hi)), lhs, rhs)
    },
    <lo: @L> <lhs: AtomicExpr> "/" <rhs: AtomicExpr> <hi: @R> => {
        Expr::div((B(lo), B(hi)), lhs, rhs)
    },
};

AtomicExpr : Expr = {
    "(" <expr: Expr> ")" => expr,
    <lo: @L> <value: Num> <hi: @R> => {
        Expr::const_((B(lo), B(hi)), value)
    },
    <lo: @L> <name: Ident> <hi: @R> => {
        Expr::var((B(lo), B(hi)), name)
    },
    <lo: @L> "-" <expr: AtomicExpr> <hi: @R> => {
        Expr::neg((B(lo), B(hi)), expr)
    },
};

Num: u32 = {
    r"[0-9]+" => u32::from_str(<>).unwrap(),
};

Field: Field = {
    <lo: @L> <name: Ident> ":" <ty: Type> <hi: @R> => {
        Field::new((B(lo), B(hi)), name, ty)
    },
};
