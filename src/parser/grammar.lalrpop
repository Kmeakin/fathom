use syntax::{Definition, Field};
use syntax::binary::Type;
use syntax::host::{Binop, Expr, Unop};
use parser::lexer::{Token, Error as LexerError};
use source::BytePos;


grammar<'input>();


// Tokens

extern {
    type Location = BytePos;
    type Error = LexerError;

    enum Token<'input> {
        // Data
        "Ident" => Token::Ident(<&'input str>),
        "BinLiteral" => Token::BinLiteral(<i64>),
        "HexLiteral" => Token::HexLiteral(<i64>),
        "DecLiteral" => Token::DecLiteral(<i64>),

        // Keywords
        "struct" => Token::Struct,
        "union" => Token::Union,
        "where" => Token::Where,

        // Symbols
        "&" => Token::Ampersand,
        "!" => Token::Bang,
        "!=" => Token::BangEqual,
        ":" => Token::Colon,
        "," => Token::Comma,
        "=" => Token::Equal,
        "==" => Token::EqualEqual,
        "=>" => Token::EqualGreater,
        "/" => Token::ForwardSlash,
        ">" => Token::Greater,
        ">=" => Token::GreaterEqual,
        "<" => Token::Less,
        "<=" => Token::LessEqual,
        "-" => Token::Minus,
        "|" => Token::Pipe,
        "+" => Token::Plus,
        ";" => Token::Semi,
        "*" => Token::Star,

        // Delimeters
        "(" => Token::LParen,
        ")" => Token::RParen,
        "{" => Token::LBrace,
        "}" => Token::RBrace,
        "[" => Token::LBracket,
        "]" => Token::RBracket,
    }
}


pub Definitions: Vec<Definition<String>> = {
    Definition*,
};

Definition: Definition<String> = {
    <name: "Ident"> "=" <ty: PrimaryType> ";" => {
        Definition::new(name, ty)
    },
};

Field: Field<String, Type<String>> = {
    <name: "Ident"> ":" <ty: PrimaryType> => {
        Field::new(name, ty)
    },
};


// Types

pub Type: Type<String> = {
    <ty: PrimaryType> => ty,
};

PrimaryType: Type<String> = {
    AtomicType,
    <ty: PrimaryType> "where" <param: "Ident"> "=>" <pred: PrimaryExpr> => {
        // FIXME: unwrap
        let repr_ty = Box::new(ty.repr().unwrap());
        Type::cond(ty, Expr::abs((param, repr_ty), pred))
    },
};

AtomicType: Type<String> = {
    <name: "Ident"> => {
        Type::fvar(name)
    },
    "(" <ty: PrimaryType> ")" => ty,
    "struct" "{" <fields: (<Field> ",")*> <last: Field?> "}" => {
        let mut fields = fields;
        fields.extend(last);
        Type::struct_(fields)
    },
    "union" "{" <tys: (<PrimaryType> ",")+> <last: PrimaryType?> "}" => {
        let mut tys = tys;
        tys.extend(last);
        Type::union(tys)
    },
    "[" <elem: PrimaryType> ";" <size: PrimaryExpr> "]" => {
        Type::array(elem, size)
    },
};


// Expressions

pub Expr: Expr<String> = {
    <expr: PrimaryExpr> => expr,
};

// Use precedence climbing to define the operators
// https://en.wikipedia.org/wiki/Operator-precedence_parser#Precedence_climbing_method

PrimaryExpr: Expr<String> = {
    EqExpr,
    <lhs: EqExpr> "|" <rhs: PrimaryExpr> => {
        Expr::binop(Binop::Or, lhs, rhs)
    },
    <lhs: EqExpr> "&" <rhs: PrimaryExpr> => {
        Expr::binop(Binop::And, lhs, rhs)
    },
};

EqExpr: Expr<String> = {
    CmpExpr,
    <lhs: CmpExpr> "==" <rhs: EqExpr> => {
        Expr::binop(Binop::Eq, lhs, rhs)
    },
    <lhs: CmpExpr> "!=" <rhs: EqExpr> => {
        Expr::binop(Binop::Ne, lhs, rhs)
    },
};

CmpExpr: Expr<String> = {
    AddExpr,
    <lhs: AddExpr> "<=" <rhs: CmpExpr> => {
        Expr::binop(Binop::Le, lhs, rhs)
    },
    <lhs: AddExpr> "<" <rhs: CmpExpr> => {
        Expr::binop(Binop::Lt, lhs, rhs)
    },
    <lhs: AddExpr> ">" <rhs: CmpExpr> => {
        Expr::binop(Binop::Gt, lhs, rhs)
    },
    <lhs: AddExpr> ">=" <rhs: CmpExpr> => {
        Expr::binop(Binop::Ge, lhs, rhs)
    },
};

AddExpr: Expr<String> = {
    MulExpr,
    <lhs: MulExpr> "+" <rhs: AddExpr> => {
        Expr::binop(Binop::Add, lhs, rhs)
    },
    <lhs: MulExpr> "-" <rhs: AddExpr> => {
        Expr::binop(Binop::Sub, lhs, rhs)
    },
};

MulExpr: Expr<String> = {
    AtomicExpr,
    <lhs: AtomicExpr> "*" <rhs: MulExpr> => {
        Expr::binop(Binop::Mul, lhs, rhs)
    },
    <lhs: AtomicExpr> "/" <rhs: MulExpr> => {
        Expr::binop(Binop::Div, lhs, rhs)
    },
};

AtomicExpr: Expr<String> = {
    "(" <expr: PrimaryExpr> ")" => expr,
    <value: "BinLiteral"> => {
        Expr::int(value)
    },
    <value: "HexLiteral"> => {
        Expr::int(value)
    },
    <value: "DecLiteral"> => {
        Expr::int(value)
    },
    <name: "Ident"> => {
        Expr::fvar(name)
    },
    "-" <expr: AtomicExpr> => {
        Expr::unop(Unop::Neg, expr)
    },
    "!" <value: AtomicExpr> => {
        Expr::unop(Unop::Not, value)
    },
};
