use ast::{Definition, Field, Expr, Type};
use env::Env;
use parser::lexer::{Token, Error as LexerError};
use source::BytePos;

grammar<'input, 'env>(env: &'env Env);

extern {
    type Location = BytePos;
    type Error = LexerError;

    enum Token<'input> {
        // Data
        "Ident" => Token::Ident(<&'input str>),
        "IntLiteral" => Token::IntLiteral(<u64>),

        // Symbols
        "=" => Token::Equals,
        ";" => Token::Semi,
        "," => Token::Comma,
        "|" => Token::Pipe,
        ":" => Token::Colon,
        "+" => Token::Plus,
        "-" => Token::Minus,
        "*" => Token::Star,
        "/" => Token::FSlash,

        // Delimeters
        "(" => Token::LParen,
        ")" => Token::RParen,
        "{" => Token::LBrace,
        "}" => Token::RBrace,
        "[" => Token::LBracket,
        "]" => Token::RBracket,
    }
}

pub Definitions: Vec<Definition> = {
    Definition*,
};

Definition: Definition = {
    <lo: @L> <name: "Ident"> "=" <ty: Type> ";" <hi: @R> => {
        Definition::new((lo, hi), name, ty)
    },
};

AtomicType: Type = {
    <lo: @L> <name: "Ident"> <hi: @R> => {
        match env.lookup_ty(&name) {
            Some(ty) => ty.clone(),
            None => Type::ident((lo, hi), name),
        }
    },
    "(" <Type> ")",
    <lo: @L> "{" <fields: (<Field> ",")*> <last: Field?> "}" <hi: @R> => {
        let mut fields = fields;
        fields.extend(last);
        Type::struct_((lo, hi), fields)
    },
    <lo: @L> "[" <elem: Type> ";" <size: Expr> "]" <hi: @R> => {
        Type::array((lo, hi), elem, size)
    },
};

pub Type: Type = {
    AtomicType,
    <lo: @L> "|"? <tys: (<AtomicType> "|")+> <last: AtomicType> <hi: @R> => {
        let mut tys = tys;
        tys.push(last);
        Type::union((lo, hi), tys)
    },
};

pub Expr = AddExpr;

AddExpr : Expr = {
    MulExpr,
    <lo: @L> <lhs: MulExpr> "+" <rhs: MulExpr> <hi: @R> => {
        Expr::add((lo, hi), lhs, rhs)
    },
    <lo: @L> <lhs: MulExpr> "-" <rhs: MulExpr> <hi: @R> => {
        Expr::sub((lo, hi), lhs, rhs)
    },
};

MulExpr : Expr = {
    AtomicExpr,
    <lo: @L> <lhs: AtomicExpr> "*" <rhs: AtomicExpr> <hi: @R> => {
        Expr::mul((lo, hi), lhs, rhs)
    },
    <lo: @L> <lhs: AtomicExpr> "/" <rhs: AtomicExpr> <hi: @R> => {
        Expr::div((lo, hi), lhs, rhs)
    },
};

AtomicExpr : Expr = {
    "(" <expr: Expr> ")" => expr,
    <lo: @L> <value: "IntLiteral"> <hi: @R> => {
        Expr::const_((lo, hi), value)
    },
    <lo: @L> <name: "Ident"> <hi: @R> => {
        Expr::var((lo, hi), name)
    },
    <lo: @L> "-" <expr: AtomicExpr> <hi: @R> => {
        Expr::neg((lo, hi), expr)
    },
};

Field: Field = {
    <lo: @L> <name: "Ident"> ":" <ty: Type> <hi: @R> => {
        Field::new((lo, hi), name, ty)
    },
};
