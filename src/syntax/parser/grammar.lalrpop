use std::iter;

use lalrpop_util::ParseError::User as UserError;
use name::Named;
use source::{BytePos, Span};
use syntax::ast::{Definition, Field, Program};
use syntax::ast::binary;
use syntax::ast::host::{self, Binop, Const, Unop};
use syntax::parser::GrammarError;
use syntax::parser::lexer::Token;
use var::Var;


grammar<'input>();


// Tokens

extern {
    type Location = BytePos;
    type Error = GrammarError;

    enum Token<'input> {
        // Data
        "Ident" => Token::Ident(<&'input str>),
        "DocComment" => Token::DocComment(<&'input str>),
        "BinLiteral" => Token::BinLiteral(<u64>, <&'input str>),
        "HexLiteral" => Token::HexLiteral(<u64>, <&'input str>),
        "DecLiteral" => Token::DecLiteral(<u64>, <&'input str>),
        "FloatDecLiteral" => Token::FloatDecLiteral(<f64>, <&'input str>),

        // Keywords
        "as" => Token::As,
        "compute" => Token::Compute,
        "from" => Token::From,
        "struct" => Token::Struct,
        "union" => Token::Union,
        "where" => Token::Where,

        // Symbols
        "&" => Token::Ampersand,
        "!" => Token::Bang,
        "!=" => Token::BangEqual,
        ":" => Token::Colon,
        "," => Token::Comma,
        "." => Token::Dot,
        "=" => Token::Equal,
        "==" => Token::EqualEqual,
        "=>" => Token::EqualGreater,
        "/" => Token::ForwardSlash,
        ">" => Token::Greater,
        ">=" => Token::GreaterEqual,
        "<" => Token::Less,
        "<=" => Token::LessEqual,
        "-" => Token::Minus,
        "|" => Token::Pipe,
        "+" => Token::Plus,
        ";" => Token::Semi,
        "*" => Token::Star,

        // Delimeters
        "(" => Token::LParen,
        ")" => Token::RParen,
        "{" => Token::LBrace,
        "}" => Token::RBrace,
        "[" => Token::LBracket,
        "]" => Token::RBracket,
    }
}


pub Program: Program<String> = {
    <defs: Definition*> => {
        Program::new(defs)
    },
};

Definition: Definition<String> = {
    <doc: "DocComment"*> <name: "Ident"> "=" <ty: PrimaryBinaryType> ";" => {
        Definition {
            doc: doc.join("\n").into(),
            name: name.to_owned(),
            ty,
        }
    },
    <doc: "DocComment"*> <name: "Ident"> <lo: @L>
        "(" <params: (<"Ident"> ",")*> <last: "Ident"> ")" "="
        <body_ty: PrimaryBinaryType> <hi: @R> ";" =>
    {
        let params = params
            .into_iter()
            .chain(iter::once(last))
            .map(|p| p.to_owned())
            .collect::<Vec<String>>();

        Definition {
            doc: doc.join("\n").into(),
            name: name.to_owned(),
            ty: binary::Type::abs(Span::new(lo, hi), &params, body_ty).into(),
        }
    },
};

Field: Field<String, binary::RcType<String>> = {
    <doc: "DocComment"*> <name: "Ident"> ":" <ty: PrimaryBinaryType> => {
        Field {
            doc: doc.join("\n").into(),
            name: name.to_owned(),
            value: ty,
        }
    },
};


// Binary Types

pub BinaryType: binary::RcType<String> = {
    <ty: PrimaryBinaryType> => ty,
};

PrimaryBinaryType: binary::RcType<String> = {
    AtomicBinaryType,
    <lo1: @L> <ty: PrimaryBinaryType> "where" <lo2: @L> <param: "Ident"> "=>" <pred: PrimaryHostExpr> <hi: @R> => {
        let repr_ty = ty.repr();
        let pred_expr = host::Expr::abs(
            Span::new(lo2, hi),
            vec![Named(param.to_owned(), repr_ty)],
            pred,
        );

        binary::Type::Assert(Span::new(lo1, hi), ty, pred_expr.into()).into()
    },
    <lo: @L> "compute" <repr_ty: HostType> "from" <expr: PrimaryHostExpr> <hi: @R> => {
        binary::Type::Compute(Span::new(lo, hi), repr_ty, expr).into()
    },
};

AtomicBinaryType: binary::RcType<String> = {
    <lo: @L> <name: "Ident"> <hi: @R> => {
        binary::Type::Var(Span::new(lo, hi), Var::free(name)).into()
    },
    <lo: @L> <ty: AtomicBinaryType> "(" <arg_tys: (<BinaryType> ",")*> <last: BinaryType> ")" <hi: @R> => {
        let mut arg_tys = arg_tys;
        arg_tys.push(last);
        binary::Type::App(Span::new(lo, hi), ty, arg_tys).into()
    },
    "(" <ty: PrimaryBinaryType> ")" => ty,
    <lo: @L> "struct" "{" <fields: (<Field> ",")*> <last: Field?> "}" <hi: @R> => {
        let mut fields = fields;
        fields.extend(last);
        binary::Type::struct_(Span::new(lo, hi), fields).into()
    },
    <lo: @L> "union" "{" <variants: (<Field> ",")*> <last: Field?> "}" <hi: @R> => {
        let mut variants = variants;
        variants.extend(last);
        binary::Type::Union(Span::new(lo, hi), variants).into()
    },
    <lo: @L> "[" <elem: PrimaryBinaryType> ";" <size: PrimaryHostExpr> "]" <hi: @R> => {
        binary::Type::Array(Span::new(lo, hi), elem, size).into()
    },
};


// Host Types

pub HostType: host::RcType<String> = {
    // FIXME: implement full host type parsing, with proper name binding
    //
    // We'll just hard-code identifiers for now
    <name: "Ident"> =>? {
        let ty_const = name.parse().map_err(|e| UserError { error: GrammarError::from(e) })?;

        Ok(host::Type::Const(ty_const).into())
    },
};


// Host Expressions

pub HostExpr: host::RcExpr<String> = {
    <expr: PrimaryHostExpr> => expr,
};

// Use precedence climbing to define the operators
// https://en.wikipedia.org/wiki/Operator-precedence_parser#Precedence_climbing_method

PrimaryHostExpr: host::RcExpr<String> = {
    EqHostExpr,
    <lo: @L> <lhs: EqHostExpr> "|" <rhs: PrimaryHostExpr> <hi: @R> => {
        host::Expr::Binop(Span::new(lo, hi), Binop::Or, lhs, rhs).into()
    },
    <lo: @L> <lhs: EqHostExpr> "&" <rhs: PrimaryHostExpr> <hi: @R> => {
        host::Expr::Binop(Span::new(lo, hi), Binop::And, lhs, rhs).into()
    },
};

EqHostExpr: host::RcExpr<String> = {
    CmpHostExpr,
    <lo: @L> <lhs: CmpHostExpr> "==" <rhs: EqHostExpr> <hi: @R> => {
        host::Expr::Binop(Span::new(lo, hi), Binop::Eq, lhs, rhs).into()
    },
    <lo: @L> <lhs: CmpHostExpr> "!=" <rhs: EqHostExpr> <hi: @R> => {
        host::Expr::Binop(Span::new(lo, hi), Binop::Ne, lhs, rhs).into()
    },
};

CmpHostExpr: host::RcExpr<String> = {
    AddHostExpr,
    <lo: @L> <lhs: AddHostExpr> "<=" <rhs: CmpHostExpr> <hi: @R> => {
        host::Expr::Binop(Span::new(lo, hi), Binop::Le, lhs, rhs).into()
    },
    <lo: @L> <lhs: AddHostExpr> "<" <rhs: CmpHostExpr> <hi: @R> => {
        host::Expr::Binop(Span::new(lo, hi), Binop::Lt, lhs, rhs).into()
    },
    <lo: @L> <lhs: AddHostExpr> ">" <rhs: CmpHostExpr> <hi: @R> => {
        host::Expr::Binop(Span::new(lo, hi), Binop::Gt, lhs, rhs).into()
    },
    <lo: @L> <lhs: AddHostExpr> ">=" <rhs: CmpHostExpr> <hi: @R> => {
        host::Expr::Binop(Span::new(lo, hi), Binop::Ge, lhs, rhs).into()
    },
};

AddHostExpr: host::RcExpr<String> = {
    MulHostExpr,
    <lo: @L> <lhs: MulHostExpr> "+" <rhs: AddHostExpr> <hi: @R> => {
        host::Expr::Binop(Span::new(lo, hi), Binop::Add, lhs, rhs).into()
    },
    <lo: @L> <lhs: MulHostExpr> "-" <rhs: AddHostExpr> <hi: @R> => {
        host::Expr::Binop(Span::new(lo, hi), Binop::Sub, lhs, rhs).into()
    },
};

MulHostExpr: host::RcExpr<String> = {
    CastHostExpr,
    <lo: @L> <lhs: CastHostExpr> "*" <rhs: MulHostExpr> <hi: @R> => {
        host::Expr::Binop(Span::new(lo, hi), Binop::Mul, lhs, rhs).into()
    },
    <lo: @L> <lhs: CastHostExpr> "/" <rhs: MulHostExpr> <hi: @R> => {
        host::Expr::Binop(Span::new(lo, hi), Binop::Div, lhs, rhs).into()
    },
};

CastHostExpr: host::RcExpr<String> = {
    PrefixHostExpr,
    <lo: @L> <expr: CastHostExpr> "as" <ty: HostType> <hi: @R> => {
        host::Expr::Cast(Span::new(lo, hi), expr, ty).into()
    },
};

PrefixHostExpr: host::RcExpr<String> = {
    AtomicHostExpr,
    <lo: @L> "-" <expr: AtomicHostExpr> <hi: @R> => {
        host::Expr::Unop(Span::new(lo, hi), Unop::Neg, expr).into()
    },
    <lo: @L> "!" <value: AtomicHostExpr> <hi: @R> => {
        host::Expr::Unop(Span::new(lo, hi), Unop::Not, value).into()
    },
};

AtomicHostExpr: host::RcExpr<String> = {
    "(" <expr: PrimaryHostExpr> ")" => expr,
    <lo: @L> <value: "BinLiteral"> <hi: @R> =>? {
        let suffix = value.1.parse().map_err(|e| UserError { error: GrammarError::from(e) })?;
        let const_int = Const::Int(value.0, suffix);

        Ok(host::Expr::Const(Span::new(lo, hi), const_int).into())
    },
    <lo: @L> <value: "HexLiteral"> <hi: @R> =>? {
        let suffix = value.1.parse().map_err(|e| UserError { error: GrammarError::from(e) })?;
        let const_int = Const::Int(value.0, suffix);

        Ok(host::Expr::Const(Span::new(lo, hi), const_int).into())
    },
    <lo: @L> <value: "DecLiteral"> <hi: @R> =>? {
        let suffix = value.1.parse().map_err(|e| UserError { error: GrammarError::from(e) })?;
        let const_int = Const::Int(value.0, suffix);

        Ok(host::Expr::Const(Span::new(lo, hi), const_int).into())
    },
    <lo: @L> <value: "FloatDecLiteral"> <hi: @R> =>? {
        let suffix = value.1.parse().map_err(|e| UserError { error: GrammarError::from(e) })?;
        let const_int = Const::Float(value.0, suffix);

        Ok(host::Expr::Const(Span::new(lo, hi), const_int).into())
    },
    <lo: @L> <name: "Ident"> <hi: @R> => {
        host::Expr::Var(Span::new(lo, hi), Var::free(name)).into()
    },
    <lo: @L> <struct_expr: AtomicHostExpr> "." <field_name: "Ident"> <hi: @R> => {
        host::Expr::Proj(Span::new(lo, hi), struct_expr, field_name.into()).into()
    },
    <lo: @L> <array_expr: AtomicHostExpr> "[" <index_expr: PrimaryHostExpr> "]" <hi: @R> => {
        host::Expr::Subscript(Span::new(lo, hi), array_expr, index_expr).into()
    },
};
