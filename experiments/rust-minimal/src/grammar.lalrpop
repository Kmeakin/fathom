use typed_arena::Arena;

use crate::StringInterner;
use crate::surface::Term;
use crate::surface::lexer::Token;

grammar<'arena, 'source>(interner: &mut StringInterner, arena: &'arena Arena<Term<'arena>>);

extern {
    type Location = usize;
    type Error = ();

    enum Token<'source> {
        "ident" => Token::Ident(<&'source str>),

        "fun" => Token::KeywordFun,
        "let" => Token::KeywordLet,
        "Type" => Token::KeywordType,

        "(" => Token::OpenParen,
        ")" => Token::CloseParen,

        ":" => Token::Colon,
        "=" => Token::Equals,
        "=>" => Token::EqualsGreater,
        "->" => Token::HyphenGreater,
        ";" => Token::Semicolon,
    }
}

pub Term : Term<'arena> = {
    FunTerm,
    "let" <def_name: "ident"> ":" <def_type: FunTerm> "=" <def_expr: FunTerm> ";" <body_expr: Term> => {
        Term::Let(
            interner.get_or_intern(def_name),
            arena.alloc(def_type),
            arena.alloc(def_expr),
            arena.alloc(body_expr),
        )
    },
};

FunTerm: Term<'arena> = {
    AppTerm,
    "("<input_name: "ident"> ":" <input_type: Term> ")" "->"  <output_type: FunTerm> => {
        Term::FunType(
            interner.get_or_intern(input_name),
            arena.alloc(input_type),
            arena.alloc(output_type),
        )
    },
    "fun" <input_name: "ident"> "=>" <output_type: FunTerm> => {
        Term::FunIntro(
            interner.get_or_intern(input_name),
            arena.alloc(output_type),
        )
    },
};

AppTerm: Term<'arena> = {
    AtomicTerm,
    <head_expr: AppTerm> <input_expr: AtomicTerm> => {
        Term::FunElim(
            arena.alloc(head_expr),
            arena.alloc(input_expr),
        )
    },
};

AtomicTerm : Term<'arena> = {
    <name: "ident"> => Term::Var(interner.get_or_intern(name)),
    "Type" => Term::Universe,
    "(" <term: Term> ")" => term,
};
