use std::sync::Arc;

use crate::lang::{FileId, Location, Located};
use crate::lang::surface::{
    Constant, FieldDeclaration, FieldDefinition, ItemData, Module, Pattern, PatternData, StructType,
    Term, TermData,
};
use crate::lang::surface::lexer::Token;
use crate::reporting::LexerMessage;

grammar<'source>(file_id: FileId);

extern {
    type Location = usize;
    type Error = LexerMessage;

    enum Token<'source> {
        "doc comment" => Token::DocComment(<String>),
        "inner doc comment" => Token::InnerDocComment(<String>),

        "name" => Token::Name(<&'source str>),
        "numeric literal" => Token::NumericLiteral(<&'source str>),
        "string literal" => Token::StringLiteral(<&'source str>),
        "character literal" => Token::CharLiteral(<&'source str>),

        "bool_elim" => Token::BoolElim,
        "const" => Token::Const,
        "else" => Token::Else,
        "f32" => Token::F32,
        "f64" => Token::F64,
        "Format" => Token::Format,
        "global" => Token::Global,
        "if" => Token::If,
        "int" => Token::Int,
        "int_elim" => Token::IntElim,
        "item" => Token::Item,
        "Kind" => Token::Kind,
        "match" => Token::Match,
        "repr" => Token::Repr,
        "struct" => Token::Struct,
        "Type" => Token::Type,

        "{" => Token::OpenBrace,
        "}" => Token::CloseBrace,
        "[" => Token::OpenBracket,
        "]" => Token::CloseBracket,
        "(" => Token::OpenParen,
        ")" => Token::CloseParen,

        "!" => Token::Bang,
        ":" => Token::Colon,
        "," => Token::Comma,
        "=" => Token::Equals,
        "=>" => Token::EqualsGreater,
        "." => Token::FullStop,
        "->" => Token::HyphenGreater,
        ";" => Token::Semi,
    }
}

pub Module: Module = {
    <doc: "inner doc comment"*> <items: Located<ItemData>*> => {
        let doc = Arc::from(doc);

        Module {
            doc: Arc::from(doc),
            items,
        }
    },
};

ItemData: ItemData = {
    <doc: "doc comment"*>
    "const" <name: Located<Name>> <type_: (":" <Term>)?> "=" <term: Term> ";" => {
        ItemData::Constant(Constant {
            doc: Arc::from(doc),
            name,
            type_,
            term,
        })
    },
    <doc: "doc comment"*>
    "struct" <name: Located<Name>> <params: Param*> <type_: (":" <Term>)?> "{"
        <fields: Separated<FieldDeclaration, ",">>
    "}" => {
        let doc = Arc::from(doc);
        let fields = fields.into();

        ItemData::StructType(StructType { doc, name, params, type_, fields })
    },
};

#[inline] Pattern: Pattern = Located<PatternData>;

PatternData: PatternData = {
    <name: Name> => PatternData::Name(name),
    <literal: "numeric literal"> => PatternData::NumberLiteral(literal.to_owned()),
};

#[inline] Term: Term = Located<TermData>;
#[inline] ArrowTerm: Term = Located<ArrowTermData>;
#[inline] AppTerm: Term = Located<AppTermData>;
#[inline] AtomicTerm: Term = Located<AtomicTermData>;

TermData: TermData = {
    ArrowTermData,
    <term: ArrowTerm> ":" <ty: Term> => TermData::Ann(Box::new(term), Box::new(ty)),
};

ArrowTermData: TermData = {
    AppTermData,
    <param_type: AppTerm> "->" <body_type: ArrowTerm> => {
        TermData::FunctionType(Box::new(param_type), Box::new(body_type))
    },
};

AppTermData: TermData = {
    AtomicTermData,
    <head: AtomicTerm> <arguments: AtomicTerm+> => {
        TermData::FunctionElim(Box::new(head), arguments)
    },
};

AtomicTermData: TermData = {
    "(" <term: TermData> ")" => term,
    <name: Name> => TermData::Name(name),
    "Type" => TermData::TypeType,
    "Kind" => TermData::KindType,
    "repr" => TermData::Repr,
    "struct" "{" <fields: Separated<FieldDefinition, ",">> "}" => TermData::StructTerm(fields),
    <term: AtomicTerm> "." <name: Located<Name>> => TermData::StructElim(Box::new(term), name),
    "[" <elem_terms: Separated<Term, ",">> "]" => TermData::SequenceTerm(elem_terms),
    <literal: "numeric literal"> => TermData::NumberLiteral(literal.to_owned()),
    "if" <head: Term> "{" <if_true: Term> "}" "else" "{" <if_false: Term> "}" => {
        TermData::If(Box::new(head), Box::new(if_true), Box::new(if_false))
    },
    "match" <head: Term> "{" <mut branches: (<Pattern> "=>" <Term> ",")*> <last: (<Pattern> "=>" <Term>)?>"}" => {
        branches.extend(last);
        TermData::Match(Box::new(head), branches)
    },
    "Format" => TermData::FormatType,
};

#[inline]
Param: (Located<String>, Term) = {
    "(" <Located<Name>> ":" <Term> ")",
};

#[inline]
FieldDeclaration: FieldDeclaration = {
    <docs: "doc comment"*> <label: Located<Name>> ":" <type_: Term> => {
        FieldDeclaration { doc: Arc::from(docs), label, type_ }
    },
};

#[inline]
FieldDefinition: FieldDefinition = {
    <label: Located<Name>> "=" <term: Term> => FieldDefinition { label, term },
};

#[inline]
Name: String = {
    <"name"> => (<>).to_owned(),
}

#[inline]
Separated<Elem, Separator>: Vec<Elem> = {
    <mut elems: (<Elem> Separator)*> <last: Elem?> => {
        elems.extend(last);
        elems
    },
};

#[inline]
Located<T>: Located<T> = {
    <start: @L> <data: T> <end: @R> => Located::new(Location::file_range(file_id, start..end), data),
};
